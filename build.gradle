buildscript {
	repositories {
		mavenCentral()
		maven {
			name = "forge"
			url = "https://files.minecraftforge.net/maven"
		}
	}
	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
	}
}

repositories {
	redserver()
	mavenCentral()
}

apply plugin: 'forge'
apply plugin: 'maven-publish'

// Project settings
archivesBaseName = 'BukkitEventsLib'
group = 'me.theandrey'
version = '1.7.10'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

minecraft {
	version = '1.7.10-10.13.4.1614-1.7.10'
	runDir = 'eclipse'
}

dependencies {
	compile group: 'org.bukkit', name: 'bukkit', version: '1.7.10-R0.1'
}

jar {
	manifest {
		attributes 'FMLCorePluginContainsFMLMod': 'true'
		attributes 'FMLCorePlugin': 'theandrey.bukkit.asm.LoadingPlugin'
	}
}

task devJar(type: Jar) {
	classifier = 'dev'
	manifest = jar.manifest
	from sourceSets.main.output
}

task sourcesJar(type: Jar) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

artifacts {
	archives devJar
	archives sourcesJar
}

publishing {
	publications {
		maven(MavenPublication) {
			from components.java
			artifact devJar
			artifact sourcesJar

			pom.withXml {
				Node pomNode = asNode()
				pomNode.dependencies.'*'.findAll() {
					it.artifactId.text() != 'bukkit'
				}.each() {
					it.parent().remove(it)
				}
			}
		}
	}
}
