buildscript {
	repositories {
		redserver()
		mavenCentral()
	}

	dependencies {
		classpath 'immibis.bon:BON:2.0@jar'
	}
}

repositories {
	redserver()
	mavenCentral()
}

apply plugin: 'java'

archivesBaseName = 'BukkitEventsLib'
group = 'theandrey'
version = '1.4.7'

compileJava {
	sourceCompatibility = JavaVersion.VERSION_1_8
	targetCompatibility = JavaVersion.VERSION_1_8
}

tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

dependencies {
	compile 'net.minecraftforge:forgeSrc:1.4.7-6.6.2.534'
	compile 'org.ow2.asm:asm-all:5.0.4'
	compile 'org.bukkit:bukkit:1.4.7-R1.0'
}

task devJar(type: Jar) {
	classifier = 'dev'
	from sourceSets.main.output
}

task sourcesJar(type: Jar) {
	classifier = 'dev-sources'
	from sourceSets.main.allSource
}

artifacts {
	archives devJar
	archives sourcesJar
}

/* Reobfuscate task */
task reobf(type: JavaExec, dependsOn: jar) {
	ext.runArgs = {
		String mcpFolder = getProperty('minecraft.mcp.dir') + '/1.4.7-forge';

		def runArgs = [
			'-m', '-mcp', mcpFolder, '-from', 'MCP', '-to', 'OBF',
			'-side', 'UNIVERSAL', '-in', jar.archivePath, '-out', jar.archivePath
		]

		// Add library path for correct obfuscation
		for (entry in configurations.runtime.resolve()) {
			if (entry.getName().contains('-dev') || entry.getName().contains('forgeSrc')) {
				runArgs.addAll('-ref', entry.getAbsolutePath());
			}
		}

		return runArgs
	}

	classpath = buildscript.configurations.classpath
	main = 'immibis.bon.cui.MCPRemap'
	args = ext.runArgs()
}

// Release mod
task release(type: Copy, dependsOn: build) {
	from devJar
	from sourcesJar
	into getProperty('minecraft.library.dir') + '/release/mods-1.4.7'
}

tasks.assemble.dependsOn tasks.reobf
