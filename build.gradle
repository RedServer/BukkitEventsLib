buildscript {
	repositories {
		mavenCentral()
		maven {
			name = 'forge'
			url = getProperty('minecraftforge.repository.url')
		}
	}
	dependencies {
		classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '6.0.+', changing: true
	}
}

repositories {
	maven {
		name = 'spigotmc'
		url = 'https://hub.spigotmc.org/nexus/repository/public'
	}
	mavenCentral()
}

apply plugin: 'java'
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'maven-publish'

// Project settings
java {
	toolchain.languageVersion = JavaLanguageVersion.of(8)
	sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8
}

tasks.withType(JavaCompile).configureEach {
	options.encoding = 'UTF-8'
}

minecraft {
	mappings channel: 'stable', version: '39-1.12'

	accessTransformer = file('src/main/resources/META-INF/bukkitevents_at.cfg')

	legacy {
		fixClasspath = true
		extractMappings = true
		attachMappings = true
	}
}

dependencies {
	minecraft 'net.minecraftforge:forge:1.12.2-14.23.5.2860'
	implementation 'org.spigotmc:spigot-api:1.12.2-R0.1-SNAPSHOT'
	implementation 'org.bukkit:bukkit:1.12.2-R0.1-SNAPSHOT'
}

jar {
	manifest {
		attributes 'FMLCorePluginContainsFMLMod': 'true'
		attributes 'FMLCorePlugin': 'theandrey.bukkit.asm.LoadingPlugin'
		attributes 'FMLAT': 'bukkitevents_at.cfg'
	}
}

tasks.register('devJar', Jar) {
	archiveClassifier.set('dev')
	manifest = jar.manifest
	from sourceSets.main.output
}

tasks.register('sourcesJar', Jar) {
	archiveClassifier.set('sources')
	from sourceSets.main.allSource
}

artifacts {
	archives tasks.devJar
	archives tasks.sourcesJar
}

publishing {
	repositories {
		maven {
			name = 'redserver'
			url = findProperty('redserver.repository.url')
			credentials() {
				username findProperty('redserver.repository.user')
				password findProperty('redserver.repository.pass')
			}
		}
	}

	publications {
		maven(MavenPublication) {
			from components.java
			artifact devJar
			artifact sourcesJar

			pom.withXml {
				Node pomNode = asNode()
				pomNode.dependencies.'*'.findAll() {
					it.artifactId.text() != 'bukkit'
				}.each() {
					it.parent().remove(it)
				}
			}
		}
	}
}
