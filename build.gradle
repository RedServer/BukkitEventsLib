buildscript {
	repositories {
		mavenCentral()
		maven {
			name = 'forge'
			url = getProperty('minecraftforge.repository.url')
		}
	}
	dependencies {
		classpath('com.anatawa12.forge:ForgeGradle:1.2-1.1.+') {
			changing = true
		}
	}
}

repositories {
	flatDir {
		dirs 'lib'
	}
	mavenCentral()
}

apply plugin: 'forge'
apply plugin: 'maven-publish'

// Project settings
sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8

tasks.withType(JavaCompile).configureEach {
	options.encoding = 'UTF-8'
}

minecraft {
	version = '1.7.10-10.13.4.1614-1.7.10'
}

dependencies {
	compile group: 'org.bukkit', name: 'bukkit', version: '1.7.10-R0.1'
}

jar {
	manifest {
		attributes 'FMLCorePluginContainsFMLMod': 'true'
		attributes 'FMLCorePlugin': 'theandrey.bukkit.asm.LoadingPlugin'
	}
}

tasks.register('devJar', Jar) {
	archiveClassifier.set('dev')
	manifest = jar.manifest
	from sourceSets.main.output
}

tasks.register('sourcesJar', Jar) {
	archiveClassifier.set('sources')
	from sourceSets.main.allSource
}

artifacts {
	archives tasks.devJar
	archives tasks.sourcesJar
}

publishing {
	repositories {
		maven {
			name = 'redserver'
			url = findProperty('redserver.repository.url')
			credentials() {
				username findProperty('redserver.repository.user')
				password findProperty('redserver.repository.pass')
			}
		}
	}

	publications {
		maven(MavenPublication) {
			from components.java
			artifact devJar
			artifact sourcesJar

			pom.withXml {
				Node pomNode = asNode()
				pomNode.dependencies.'*'.findAll() {
					it.artifactId.text() != 'bukkit'
				}.each() {
					it.parent().remove(it)
				}
			}
		}
	}
}
